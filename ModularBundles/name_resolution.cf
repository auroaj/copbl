
bundle agent name_resolution

{
files:

  "$(sys.resolv)"  # test on "/tmp/resolv.conf" #

     comment       => "Add appropriate configuration to the resolver (DNS)",
     create        => "true",
     edit_line     => resolver,
     edit_defaults => std_edits;

}

#######################################################

bundle edit_line resolver

{
delete_lines:

  # Clean up old junk, if you like

  "search.*";
  "nameserver 80.65.58.31";
  "nameserver 80.65.58.32";
  "nameserver 82.103.128.146";
  "nameserver 78.24.145.4";
  "nameserver 78.24.145.5";
  "nameserver 128.39.89.10";

insert_lines:

 any::

  "search cfengine.com" 

      comment => "Add the search domains separated by spaces",
     location => start;

  #
  # Now add the nameservers for different classes of host
  # Best to keep this special bundle for doing this, rather
  # than try to abstract away, as this just *is* messy
  #

 west_wing::

  "nameserver 128.39.89.8";
  "nameserver 128.39.74.66";

 nameservers::

  "nameserver 127.0.0.1";

 any::

  "nameserver 212.112.166.18";
  "nameserver 212.112.166.22";
}



