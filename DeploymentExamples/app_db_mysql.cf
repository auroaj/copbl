

bundle agent app_db_mysql
{
vars:

  SuSE::

    "match_package" slist => { 
                             "mysql",
                             };
  redhat::

    "match_package" slist => {
                             "mysql",
                             "mysql-server",
                             };
  debian::
 
   "match_package" slist => {
                            "mysql-server",
                            };

packages:

   "$(match_package)"

         package_policy => "add",
         package_method => generic;

processes:

  "mysqld" -> "start_mysql",

       restart_class => "start_mysql",
             comment => "Make sure the back-end mySQL database is running";

commands:

 start_mysql::

   "/etc/init.d/mysql restart",

       handle => "start_mysql",
      comment => "Start the mysqld service";

    "/usr/bin/mysqldump --all-databases > /var/cfengine/reports/mysql_dump.xml"

         comment => "Dump all MYSQL databases to xml for backup purposes",
         contain => in_shell,
          action => if_elapsed("8460");
}

